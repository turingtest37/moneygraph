PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX mg: <https://w3id.org/moneygraph/ns/ontology/>
PREFIX mg3: <https://w3id.org/moneygraph/ns/data/>
PREFIX mgx: <https://w3id.org/moneygraph/ns/taxonomy/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ofn: <http://www.ontotext.com/sparql/functions/>

SELECT DISTINCT (?__report_datetime AS ?as_of) ?bond_iri ?bond (?coupon*100 AS ?coupon_) ?price ?my_price (?my_price-?price AS ?spread) ?yr ?maturity ?effective_maturity (?ytm*100 AS ?yield_to_mat) ?issuer ?rating ?callable

FROM mg3:__bondlist__
FROM mg3:__bondlist__extras

{
    [] gist:atDateTime ?__report_datetime ;
    	gist:isAbout ?bond_iri ;
    .
    ?bond_iri a mg:Bond ; 
        skos:prefLabel ?bond ; 
        mg:isIssuedBy/gist:name ?issuer ;
        mg:hasRepaymentTerms ?repay ;
        mg:hasInterestPaymentTerms ?interestterms ;
    .
    ?interestterms gist:specifies ?coupon_iri .
    ?coupon_iri a mg:BondCoupon ; gist:numericValue ?coupon .
    ?repay gist:specifies [ gist:isCharacterizedAs mgx:_FinancialBusinessEventType_debtPrincipalRepayment ; mg:plannedOccursDate ?maturity ] ;
            mg:callable ?callable ;
    	.
    OPTIONAL { ?repay gist:specifies [ mg:plannedOccursDate ?effective_maturity ; gist:isCharacterizedAs mgx:_FinancialBusinessEventType_callable_debtPrincipalRepayment] }

    # Daily changing things (price, YTM)
    ?_evt a gist:RevaluationEvent ;
        gist:isAbout ?bond_iri ;
        gist:hasMagnitude [ gist:isCategorizedBy mgx:_Aspect_yieldToMaturity ; gist:numericValue ?ytm ] ;
        gist:hasMagnitude [ gist:isCategorizedBy mgx:_Aspect_securityShareUnitPriceAmount ; gist:numericValue ?price ] ;
        gist:hasMagnitude [gist:numericValue ?npv_coupons ; gist:isCategorizedBy mgx:_Aspect_npv_simulated_bond_coupons ] ;
        gist:hasMagnitude [gist:numericValue ?npv_principal ; gist:isCategorizedBy mgx:_Aspect_npv_simulated_bond_principal ] ;
        gist:hasMagnitude [gist:numericValue ?npv_total ; gist:isCategorizedBy mgx:_Aspect_npv_simulated_bond_value ] ;
        gist:hasMagnitude [gist:numericValue ?my_price ; gist:isCategorizedBy mgx:_Aspect_simulated_bond_price_realized_yield ] ;
        gist:hasMagnitude [gist:numericValue ?net_cost ; gist:isCategorizedBy mgx:_Aspect_simulated_bond_position_cost] ;
    .
    OPTIONAL { ?_evt gist:isAffectedBy/skos:prefLabel ?rating }
    BIND(YEAR(?maturity) AS ?yr)
} ORDER BY ASC(?yr) DESC(?spread)  
